CONFIG += c++14 link_prl warn_on

PROJECT_IMPORTS_DIR = $$shadowed(imports)
PROJECT_LIBRARY_DIR = $$shadowed(lib)

defineReplace(sharedLibraryPath) {
    return($$PROJECT_LIBRARY_DIR/$${QMAKE_PREFIX_SHLIB}$${1}.$${QMAKE_EXTENSION_SHLIB})
}

defineTest(linkSharedLibrary) {
    PRE_TARGETDEPS *= $$sharedLibraryPath($$1)
    export(PRE_TARGETDEPS)

    LIBS *= -L$$PROJECT_LIBRARY_DIR
    LIBS *= -l$$1
    export(LIBS)
}

defineReplace(staticLibraryPath) {
    return($$PROJECT_LIBRARY_DIR/$${QMAKE_PREFIX_STATICLIB}$${1}.$${QMAKE_EXTENSION_STATICLIB})
}

defineTest(linkStaticLibrary) {
    PRE_TARGETDEPS *= $$staticLibraryPath($$1)
    export(PRE_TARGETDEPS)

    LIBS *= -l$$1
    export(LIBS)
}

defineTest(buildQmlPlugin) {
    MODULE_URI = $$1
    MODULE_VERSION = $$2

    DESTDIR = $$PROJECT_IMPORTS_DIR/$$replace(MODULE_URI, \., /)
    export(DESTDIR)

    QMAKE_MOC_OPTIONS += -Muri=$$MODULE_URI
    export(QMAKE_MOC_OPTIONS)

    QMAKE_POST_LINK += $$QMAKE_COPY $$PWD/qmldir $$DESTDIR$$escape_expand(\\n\\t)

    !cross_compile {
        qtPrepareTool(QMLPLUGINDUMP, qmlplugindump)

        QMAKE_POST_LINK += \
            QML_DISABLE_DISK_CACHE=1 \
            $$QMLPLUGINDUMP -output $$DESTDIR/$${TARGET}.qmltypes \
            $$MODULE_URI $$MODULE_VERSION $$PROJECT_IMPORTS_DIR \
            $$escape_expand(\\n\\t)
    }

    export(QMAKE_POST_LINK)
}
